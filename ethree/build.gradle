plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

group 'com.virgilsecurity'
version '0.1.0-aplha'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Virgil
    compileOnly "com.virgilsecurity.sdk:crypto:${versions.virgilSdk}"
    implementation "com.virgilsecurity.sdk:crypto-android:${versions.virgilSdk}@aar"
    implementation "com.virgilsecurity.sdk:sdk:$versions.virgilSdk"
    implementation ('com.virgilsecurity:pythia:0.2.0') {
        exclude group: 'com.virgilsecurity.sdk', module: 'crypto'
    }
    implementation ('com.virgilsecurity:keyknox:0.1.0') {
        exclude group: 'com.virgilsecurity.sdk', module: 'crypto'
    }

    // Android
    compileOnly "com.google.android:android:$versions.android"
}

sourceCompatibility = "8"
targetCompatibility = "8"

task sourcesJar(type: Jar) {
    from(sourceSets["main"].allSource)
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: 'dokka') {
    from "$buildDir/javadoc"
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'ethree-kotlin'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Virgil E3Kit Kotlin/Java SDK'
                description = 'Virgil Security provides an SDK which symplifies work with Virgil services and presents easy to use API for adding security to any application. In a few simple steps you can setup user encryption with multidevice support.'
                url = 'https://www.virgilsecurity.com/'
                licenses {
                    license {
                        name = 'Virgil Security, Inc. license'
                        url = 'https://github.com/VirgilSecurity/e3kit-kotlin/blob/master/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'BuddahLD'
                        name = 'Danylo Oliinyk'
                        email = 'doliinyk@virgilsecurity.com'
                        organizationUrl = 'https://github.com/BuddahLD'
                    }
                    developer {
                        id = 'andrii-iakovenko'
                        name = 'Andrii Iakovenko'
                        email = 'andrii-iakovenko@gmail.com'
                        organizationUrl = 'https://github.com/andrii-iakovenko'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/VirgilSecurity/e3kit-kotlin.git'
                    developerConnection = 'scm:git:git@github.com:VirgilSecurity/e3kit-kotlin.git'
                    url = 'https://github.com/VirgilSecurity/e3kit-kotlin'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "${authentication_username}"
                password "${authentication_password}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    reportUndocumented = false
}
