buildscript {
    ext.versions = [
            // Virgil
            virgilSdk : '5.0.4',
            pythia : '0.2.0',
            keyknox : '0.1.1',

            // Kotlin
            kotlin    : '1.3.0',
            coroutines: '1.0.0-RC1',

            // Gradle
            gardle    : '3.2.1',

            // Android
            android   : '4.1.1.4',

            // Docs
            dokka     : '0.9.17',

            // Tests
            junit     : '5.2.0',
    ]
    repositories {
        google()
        jcenter()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.gardle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenLocal()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

enum ArtifactType {
    ALL,
    STANDARD,
    COROUTINES
}

static String getArtifactType(Project project) {
    def systemProperty = null
    if (System.getenv('ARTIFACT_TYPE') != null)
        systemProperty = System.getenv('ARTIFACT_TYPE')
    if (System.getProperty('ARTIFACT_TYPE') != null)
        systemProperty = System.getProperty('ARTIFACT_TYPE')

    if (systemProperty != null)
        return systemProperty
    else if (project.hasProperty('ARTIFACT_TYPE')) {
        return project.findProperty('ARTIFACT_TYPE')
    } else {
        return "No artifact type is provided. Please, use -DARTIFACT_TYPE property."
    }
}

task installEthree() {
    def artifactType
    try {
        artifactType = getArtifactType(project) as ArtifactType
    } catch (IllegalArgumentException ignored) {
        throw new IllegalArgumentException("Please, choose one of the available types: " + ArtifactType.values() + "."
                + " You entered: " + getArtifactType(project))
    }

    if (artifactType == ArtifactType.STANDARD) {
        dependsOn ':ethree:install'
    } else if (artifactType == ArtifactType.COROUTINES) {
        dependsOn ':ethreeCoroutines:install'
    } else if (artifactType == ArtifactType.ALL) {
        dependsOn ':ethree:install', ':ethreeCoroutines:install'
    } else {
        throw new IllegalArgumentException("Please, choose one of artifacts type: " + ArtifactType.values())
    }
}

task publishEthree() {
    def artifactType
    try {
        artifactType = getArtifactType(project) as ArtifactType
    } catch (IllegalArgumentException ignored) {
        throw new IllegalArgumentException("Please, choose one of the available types: " + ArtifactType.values() + "."
                + " You entered: " + getArtifactType(project))
    }

    if (artifactType == ArtifactType.STANDARD) {
        dependsOn ':ethree:publish'
    } else if (artifactType == ArtifactType.COROUTINES) {
        dependsOn ':ethreeCoroutines:publish'
    } else if (artifactType == ArtifactType.ALL) {
        dependsOn ':ethree:publish', ':ethreeCoroutines:publish'
    } else {
        throw new IllegalArgumentException("Please, choose one of artifacts type: " + ArtifactType.values())
    }
}
