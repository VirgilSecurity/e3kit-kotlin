apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def APP_ID = hasProperty('APP_ID') ? APP_ID : System.getenv('APP_ID')
def API_PRIVATE_KEY = hasProperty('API_PRIVATE_KEY') ? API_PRIVATE_KEY : System.getenv('API_PRIVATE_KEY')
def API_PUBLIC_KEY = hasProperty('API_PUBLIC_KEY') ? API_PUBLIC_KEY : System.getenv('API_PUBLIC_KEY')
def API_PUBLIC_KEY_ID = hasProperty('API_PUBLIC_KEY_ID') ? API_PUBLIC_KEY_ID : System.getenv('API_PUBLIC_KEY_ID')
def VIRGIL_BASE_URL = hasProperty('VIRGIL_BASE_URL') ? VIRGIL_BASE_URL : System.getenv('VIRGIL_BASE_URL')

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.virgilsecurity.ethree"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        buildTypes.each {
            it.buildConfigField "String", "APP_ID", "$APP_ID"
            it.buildConfigField "String", "API_PRIVATE_KEY", "$API_PRIVATE_KEY"
            it.buildConfigField "String", "API_PUBLIC_KEY", "$API_PUBLIC_KEY"
            it.buildConfigField "String", "API_PUBLIC_KEY_ID", "$API_PUBLIC_KEY_ID"
            it.buildConfigField "String", "VIRGIL_BASE_URL", "$VIRGIL_BASE_URL"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':ethree-common')
    implementation project(':ethree-kotlin')

    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
    implementation 'com.android.support:appcompat-v7:28.0.0'

    // Virgil
    implementation "com.virgilsecurity.sdk:crypto-android:${versions.virgilSdk}@aar"
    implementation "com.virgilsecurity.sdk:sdk:$versions.virgilSdk"
    implementation ("com.virgilsecurity:pythia:$versions.pythia") {
        exclude group: 'com.virgilsecurity.sdk', module: 'crypto'
    }
    implementation ("com.virgilsecurity:keyknox:$versions.keyknox") {
        exclude group: 'com.virgilsecurity.sdk', module: 'crypto'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }

    // Tests
    testImplementation "junit:junit:$versions.junit"
    androidTestImplementation "com.android.support.test:runner:$versions.testsRunner"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espresso"
}
