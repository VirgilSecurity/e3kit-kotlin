plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

group 'com.virgilsecurity'
version '0.2.1'

dependencies {
    // Inner dependencies
    implementation project(':ethree-common')

    // Virgil
    compileOnly "com.virgilsecurity.sdk:crypto:$versions.virgilSdk"
    implementation "com.virgilsecurity.sdk:crypto-android:$versions.virgilSdk@aar"
    implementation "com.virgilsecurity.sdk:sdk:$versions.virgilSdk"
    implementation ("com.virgilsecurity:pythia:$versions.pythia") {
        exclude group: 'com.virgilsecurity.sdk', module: 'crypto'
    }
    implementation ("com.virgilsecurity:keyknox:$versions.keyknox") {
        exclude group: 'com.virgilsecurity.sdk', module: 'crypto'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"

    // Android
    compileOnly "com.google.android:android:$versions.android"
}

sourceCompatibility = "8"
targetCompatibility = "8"

task sourcesJar(type: Jar) {
    from(sourceSets["main"].allSource)
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: 'dokka') {
    from "$buildDir/javadoc"
    classifier = 'javadoc'
}

def authentication_username = hasProperty('authentication_username') ? authentication_username : System.getenv('authentication_username')
def authentication_password = hasProperty('authentication_password') ? authentication_password : System.getenv('authentication_password')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'ethree-kotlin-coroutines'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Virgil E3Kit Kotlin Coroutines SDK'
                description = 'Virgil Security provides an SDK which symplifies work with Virgil services and presents easy to use API for adding security to any application. In a few simple steps you can setup user encryption with multidevice support.'
                url = 'https://www.virgilsecurity.com/'
                licenses {
                    license {
                        name = 'Virgil Security, Inc. license'
                        url = 'https://github.com/VirgilSecurity/e3kit-kotlin/blob/master/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'BuddahLD'
                        name = 'Danylo Oliinyk'
                        email = 'doliinyk@virgilsecurity.com'
                        organizationUrl = 'https://github.com/BuddahLD'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/VirgilSecurity/e3kit-kotlin.git'
                    developerConnection = 'scm:git:git@github.com:VirgilSecurity/e3kit-kotlin.git'
                    url = 'https://github.com/VirgilSecurity/e3kit-kotlin'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "${authentication_username}"
                password "${authentication_password}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    reportUndocumented = false
}
